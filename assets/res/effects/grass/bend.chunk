
#pragma builtin(global)
layout(set = 0, binding = 4) uniform sampler2D cc_grass_bend_map;

#pragma builtin(global)
layout(set = 0, binding = 5) uniform CCGrassBend {
    vec4 cc_grass_bend_uv;
};

struct BendSettings
{
	int mode;
	float mask;
	float pushStrength;
	float flattenStrength;
	float perspectiveCorrection;
};


//Bend map UV
vec2 getBendMapUV(in vec3 wPos) {
	// vec2 uv = cc_grass_bend_uv.xy / cc_grass_bend_uv.z + wPos.xz / cc_grass_bend_uv.z;//(cc_grass_bend_uv.z / (cc_grass_bend_uv.z * cc_grass_bend_uv.z)) * wPos.xz;

    vec2 uv = (wPos.xz - cc_grass_bend_uv.xy) / cc_grass_bend_uv.z + 0.5;

	//Since version 7.3.1, UV must be flipped
    // #if VERSION_GREATER_EQUAL(7,4)
    // 	uv.y = 1 - uv.y;
    // #endif

	return uv;			
}

vec4 getBendVector(vec3 wPos) 
{
	if (cc_grass_bend_uv.w == 0.) {
        return vec4(0.5, wPos.y, 0.5, 0.0);
    }

	vec2 uv = getBendMapUV(wPos);

	vec4 v = texture(cc_grass_bend_map, uv);

	//Remap from 0.1 to -1.1
	v.x = v.x * 2.0 - 1.0;
	v.z = v.z * 2.0 - 1.0;

	return v;
}


vec4 getBendOffset(vec3 wPos, BendSettings b) {
	vec4 vec = getBendVector(wPos);

	vec4 offset = vec4(wPos, vec.a);

	float grassHeight = wPos.y;
	float bendHeight = vec.y;
	float dist = grassHeight - bendHeight;

	//Note since 7.1.5 somehow this causes the grass to bend down after the bender reaches a certain height
	// dist = abs(dist); //If bender is below grass, dont bend up

	float weight = clamp(dist, 0., 1.);


	offset.xz = vec.xz * b.mask * weight * b.pushStrength;
	offset.y = b.mask * (vec.a * 0.75) * weight * b.flattenStrength;

	float influence = 1.;

	//Pass the mask, so it can be used to lerp between wind and bend offset vectors
	offset.a = vec.a * weight * influence;

	//Apply mask
	offset.xyz *= offset.a;

	return offset;
}
